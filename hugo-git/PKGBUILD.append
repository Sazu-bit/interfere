optdepends=('python-pygments: syntax-highlight code snippets'
            'python-docutils: reStructuredText support')

build() {
  cd "${srcdir}/${_pkgname}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -tags extended

  ./hugo gen man
  ./hugo gen autocomplete --type bash --completionfile ${_pkgname}.bash-completion
  ./hugo gen autocomplete --type zsh --completionfile ${_pkgname}.zsh
}

package() {
  cd "${srcdir}/${_pkgname}"
  install -Dm755 "${_pkgname}" "${pkgdir}"/usr/bin/${_pkgname}
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${_pkgname}/LICENSE

  install -Dm644 "${srcdir}"/${_pkgname}-${pkgver}/man/*.1  -t "${pkgdir}"/usr/share/man/man1/

  install -Dm644 ${_pkgname}.bash-completion "${pkgdir}"/usr/share/bash-completion/completions/${_pkgname}
  install -Dm644 ${_pkgname}.zsh "${pkgdir}"/usr/share/zsh/site-functions/_${_pkgname}
}
